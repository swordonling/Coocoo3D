#ifndef RAYTRACING_HLSL
#define RAYTRACING_HLSL
#include "../Shaders/BRDF/PBR.hlsli"
#include "../Shaders/CameraDataDefine.hlsli"
#include "../Shaders/RandomNumberGenerator.hlsli"

typedef BuiltInTriangleIntersectionAttributes TriAttributes;
struct RayPayload
{
	float4 color;
	float3 direction;
	uint depth;
};
static const int c_testRayIndex = 1;
struct TestRayPayload
{
	bool miss;
	float3 hitPos;
	float4 color;
};

struct LightInfo
{
	float3 LightDir;
	uint LightType;
	float4 LightColor;
};

struct VertexSkinned
{
	float3 Pos;
	float3 Norm;
	float2 Tex;
	float3 Tangent;
	float EdgeScale;
	float4 preserved1;
};

struct Ray
{
	float3 origin;
	float3 direction;
};

RaytracingAccelerationStructure Scene : register(t0);
TextureCube EnvCube : register (t1);
TextureCube IrradianceCube : register (t2);
Texture2D BRDFLut : register(t3);
Texture2D ShadowMap0 : register(t4);
RWTexture2D<float4> g_renderTarget : register(u0);
cbuffer cb0 : register(b0)
{
	CAMERA_DATA_DEFINE;//is a macro
	float4x4 LightSpaceMatrices[1];
};
//local
StructuredBuffer<VertexSkinned> Vertices : register(t1, space1);
Texture2D diffuseMap :register(t2, space1);
SamplerState s0 : register(s0);
SamplerState s1 : register(s1);
SamplerComparisonState sampleShadowMap0 : register(s2);
//
cbuffer cb3 : register(b3)
{
	float4 _DiffuseColor;
	float4 _SpecularColor;
	float3 _AmbientColor;
	float _EdgeScale;
	float4 _EdgeColor;

	float4 _Texture;
	float4 _SubTexture;
	float4 _ToonTexture;
	uint notUse;
	float _Metallic;
	float _Roughness;
	float _Emission;
	float _Subsurface;
	float _Specular;
	float _SpecularTint;
	float _Anisotropic;
	float _Sheen;
	float _SheenTint;
	float _Clearcoat;
	float _ClearcoatGloss;
	float3 preserved0;
	float4 preserved1[5];
	LightInfo Lightings[8];
};

inline Ray GenerateCameraRay(uint2 index, in float3 cameraPosition, in float4x4 projectionToWorld)
{
	float2 xy = index + 0.5f; // center in the middle of the pixel.
	float2 screenPos = xy / DispatchRaysDimensions().xy * 2.0 - 1.0;

	screenPos.y = -screenPos.y;

	float4 world = mul(float4(screenPos, 0, 1), projectionToWorld);
	world.xyz /= world.w;

	Ray ray;
	ray.origin = cameraPosition;
	ray.direction = normalize(world.xyz - ray.origin);

	return ray;
}

[shader("raygeneration")]
void MyRaygenShader()
{
	Ray ray = GenerateCameraRay(DispatchRaysIndex().xy, g_vCamPos.xyz, g_mProjToWorld);

	uint currentRecursionDepth = 0;


	RayDesc ray2;
	ray2.Origin = ray.origin;
	ray2.Direction = ray.direction;
	ray2.TMin = 0.001;
	ray2.TMax = 10000.0;
	RayPayload payload = { float4(0, 0, 0, 0),ray.direction,0 };
	TraceRay(Scene, RAY_FLAG_NONE, ~0, 0, 2, 0, ray2, payload);
	g_renderTarget[DispatchRaysIndex().xy] = payload.color;
}

[shader("anyhit")]
void AnyHitShaderSurface(inout RayPayload payload, in TriAttributes attr)
{
	VertexSkinned triVert[3];
	triVert[0] = Vertices[PrimitiveIndex() * 3];
	triVert[1] = Vertices[PrimitiveIndex() * 3 + 1];
	triVert[2] = Vertices[PrimitiveIndex() * 3 + 2];

	float2 uv = triVert[0].Tex * (1 - attr.barycentrics.x - attr.barycentrics.y) +
		triVert[1].Tex * (attr.barycentrics.x) +
		triVert[2].Tex * (attr.barycentrics.y);
	float4 diffuseColor = diffuseMap.SampleLevel(s0, uv, 0) * _DiffuseColor;
	if (diffuseColor.a < 0.01)
	{
		IgnoreHit();
	}
}
float3 RandomVecImp(inout uint randomState, float3 N, out float weight)
{
	float3 randomVec = normalize(float3(RNG::NDRandom(randomState), RNG::NDRandom(randomState), RNG::NDRandom(randomState)));
	if (dot(randomVec, N) < 0)
	{
		randomVec = -randomVec;
	}
	weight = 1;
	return randomVec;
}
[shader("closesthit")]
void ClosestHitShaderSurface(inout RayPayload payload, in TriAttributes attr)
{
	payload.depth++;
	VertexSkinned triVert[3];
	triVert[0] = Vertices[PrimitiveIndex() * 3];
	triVert[1] = Vertices[PrimitiveIndex() * 3 + 1];
	triVert[2] = Vertices[PrimitiveIndex() * 3 + 2];
	float3 triNorm = normalize(cross(triVert[0].Pos - triVert[1].Pos, triVert[1].Pos - triVert[2].Pos));
	float2 uv = triVert[0].Tex * (1 - attr.barycentrics.x - attr.barycentrics.y) +
		triVert[1].Tex * (attr.barycentrics.x) +
		triVert[2].Tex * (attr.barycentrics.y);
	float3 pos = (triVert[0].Pos * (1 - attr.barycentrics.x - attr.barycentrics.y) +
		triVert[1].Pos * (attr.barycentrics.x) +
		triVert[2].Pos * (attr.barycentrics.y)).xyz;
	float3 normal = triVert[0].Norm * (1 - attr.barycentrics.x - attr.barycentrics.y) +
		triVert[1].Norm * (attr.barycentrics.x) +
		triVert[2].Norm * (attr.barycentrics.y);

	float4 diffuseColor = diffuseMap.SampleLevel(s0, uv, 0) * _DiffuseColor;

	normal = normalize(normal);


	float3 V = -payload.direction;
	float3 N = normalize(normal);
	if (dot(normal, V) < 0)
	{
		N = -N;
	}
	float NdotV = saturate(dot(N, V));

	// Burley roughness bias
	float alpha = _Roughness * _Roughness;

	float roughness = _Roughness;
	float3 albedo = diffuseColor.rgb;
	float xxx = (_Specular * 0.08f + _Metallic * (1 - _Specular * 0.08f));

	float3 c_diffuse = lerp(albedo * (1 - _Specular * 0.08f), 0, _Metallic);
	float3 c_specular = lerp(_Specular * 0.08f, albedo, _Metallic);

	float3 outputColor = float3(0, 0, 0);

	uint randomState = RNG::RandomSeed(DispatchRaysIndex().x + DispatchRaysIndex().y * 8192 + g_camera_randomValue);
	float3 AOFactor = 1.0f;
	//if (payload.depth == 1 && g_enableAO != 0)
	//{
	//	static const float c_AOMaxDist = 32;
	//	int aoSampleCount = pow(2, g_quality) * 8;
	//	for (int i = 0; i < aoSampleCount; i++)
	//	{
	//		RayDesc ray2;
	//		float startPosOffsetN = 0;
	//		for (int j = 0; j < 3; j++)
	//		{
	//			startPosOffsetN = max(dot(triVert[j].Pos - pos, normal), startPosOffsetN);
	//		}
	//		ray2.Origin = pos + normal * startPosOffsetN;
	//		ray2.Direction = normalize(float3(RNG::NDRandom(randomState), RNG::NDRandom(randomState), RNG::NDRandom(randomState)));
	//		if (dot(ray2.Direction, normal) < 0)
	//		{
	//			ray2.Direction = -ray2.Direction;
	//		}
	//		ray2.TMin = 1e-3f;
	//		ray2.TMax = c_AOMaxDist;
	//		TestRayPayload payload2 = { false,float3(0,0,0),float4(0,0,0,0) };
	//		TraceRay(Scene, RAY_FLAG_NONE, ~0, c_testRayIndex, 2, c_testRayIndex, ray2, payload2);
	//		if (!payload2.miss)
	//		{
	//			AOFactor -= (c_AOMaxDist - distance(payload2.hitPos, pos)) * (1 - payload2.color * 0.3) / c_AOMaxDist / aoSampleCount;
	//		}
	//	}
	//}
	if (g_enableShadow != 0)
	{
		[loop]
		for (int i = 0; i < 8; i++)
		{
			if (Lightings[i].LightColor.r > 0 || Lightings[i].LightColor.g > 0 || Lightings[i].LightColor.b > 0)
			{
				float inShadow = 1.0f;
				float3 lightStrength;
				float3 L;
				if (Lightings[i].LightType == 0)
				{
					lightStrength = max(Lightings[i].LightColor.rgb * Lightings[i].LightColor.a, 0);
					if (payload.depth > 1 && i == 0)
					{
						float4 sPos = mul(float4(pos, 1), LightSpaceMatrices[0]);
						float2 shadowTexCoords;
						shadowTexCoords.x = 0.5f + (sPos.x / sPos.w * 0.5f);
						shadowTexCoords.y = 0.5f - (sPos.y / sPos.w * 0.5f);
						if (saturate(shadowTexCoords.x) - shadowTexCoords.x == 0 && saturate(shadowTexCoords.y) - shadowTexCoords.y == 0 && g_enableShadow != 0)
							inShadow = ShadowMap0.SampleCmpLevelZero(sampleShadowMap0, shadowTexCoords, sPos.z / sPos.w).r;
						else
						{
							RayDesc ray2;
							ray2.Origin = pos;
							ray2.Direction = Lightings[i].LightDir;
							ray2.TMin = 0.001;
							ray2.TMax = 10000.0;
							TestRayPayload payload2 = { false, float3(0,0,0), float4(0,0,0,0) };
							if (payload.depth < 4 && dot(lightStrength, lightStrength)>1e-3)
								TraceRay(Scene, RAY_FLAG_NONE, ~0, c_testRayIndex, 2, c_testRayIndex, ray2, payload2);
							else
								continue;
							if (!payload2.miss)
								inShadow = 0;
						}
					}
					else
					{
						RayDesc ray2;
						ray2.Origin = pos;
						ray2.Direction = Lightings[i].LightDir;
						ray2.TMin = 0.001;
						ray2.TMax = 10000.0;
						TestRayPayload payload2 = { false, float3(0,0,0), float4(0,0,0,0) };
						if (payload.depth < 4 && dot(lightStrength, lightStrength)>1e-3)
							TraceRay(Scene, RAY_FLAG_NONE, ~0, c_testRayIndex, 2, c_testRayIndex, ray2, payload2);
						else
							continue;
						if (!payload2.miss)
							inShadow = 0;
					}

					L = normalize(Lightings[i].LightDir);
				}
				else if (Lightings[i].LightType == 1)
				{
					lightStrength = Lightings[i].LightColor.rgb * Lightings[i].LightColor.a / pow(distance(Lightings[i].LightDir, pos), 2);
					RayDesc ray2;
					ray2.Origin = pos;
					ray2.Direction = normalize(Lightings[i].LightDir - pos);
					ray2.TMin = 0.001;
					ray2.TMax = distance(Lightings[i].LightDir, pos);
					TestRayPayload payload2 = { false,float3(0,0,0),float4(0,0,0,0) };
					if (payload.depth < 4 && dot(lightStrength, lightStrength)>1e-3)
						TraceRay(Scene, RAY_FLAG_NONE, ~0, c_testRayIndex, 2, c_testRayIndex, ray2, payload2);
					else
						continue;

					if (!payload2.miss)
						inShadow = 0.0f;

					L = normalize(Lightings[i].LightDir - pos);

				}
				float3 H = normalize(L + V);
				float3 NdotL = saturate(dot(N, L));
				float3 LdotH = saturate(dot(L, H));
				float3 NdotH = saturate(dot(N, H));

				float diffuse_factor = Diffuse_Lambert(NdotL, NdotV, LdotH, _Roughness);
				float3 specular_factor = Specular_BRDF(alpha, c_specular, NdotV, NdotL, LdotH, NdotH);

				outputColor += NdotL * lightStrength * (((c_diffuse * diffuse_factor / COO_PI) + specular_factor)) * inShadow;
			}
		}
	}
	else
	{
		[loop]
		for (int i = 0; i < 8; i++)
		{
			if (Lightings[i].LightColor.r > 0 || Lightings[i].LightColor.g > 0 || Lightings[i].LightColor.b > 0)
			{
				float inShadow = 1.0f;
				float3 lightStrength;
				float3 L;
				if (Lightings[i].LightType == 0)
				{
					lightStrength = max(Lightings[i].LightColor.rgb * Lightings[i].LightColor.a, 0);

					L = normalize(Lightings[i].LightDir);
				}
				else if (Lightings[i].LightType == 1)
				{
					lightStrength = Lightings[i].LightColor.rgb * Lightings[i].LightColor.a / pow(distance(Lightings[i].LightDir, pos), 2);

					L = normalize(Lightings[i].LightDir - pos);
				}
				float3 H = normalize(L + V);
				float3 NdotL = saturate(dot(N, L));
				float3 LdotH = saturate(dot(L, H));
				float3 NdotH = saturate(dot(N, H));

				float diffuse_factor = Diffuse_Burley(NdotL, NdotV, LdotH, _Roughness);
				float3 specular_factor = Specular_BRDF(alpha, c_specular, NdotV, NdotL, LdotH, NdotH);

				outputColor += NdotL * lightStrength * (((c_diffuse * diffuse_factor / COO_PI) + specular_factor)) * inShadow;
			}
		}
	}


	float surfaceReduction = 1.0f / (roughness * roughness + 1.0f);
	float grazingTerm = saturate(1 - sqrt(roughness) + xxx);
	float2 AB = BRDFLut.SampleLevel(s0, float2(NdotV, roughness), 0).rg;

	if (payload.depth < 2 && g_enableAO != 0 && (c_diffuse.r > 0.02f || c_diffuse.g > 0.02f || c_diffuse.b > 0.02f))
	{
		int giSampleCount = pow(2, g_quality) * 8;
		float3 gi = 0;
		for (int i = 0; i < giSampleCount; i++)
		{
			RayPayload payloadGI;
			payloadGI.direction = normalize(float3(RNG::NDRandom(randomState), RNG::NDRandom(randomState), RNG::NDRandom(randomState)));
			if (dot(payloadGI.direction, N) < 0)
			{
				payloadGI.direction = -payloadGI.direction;
			}
			payloadGI.color = float4(0, 0, 0, 0);
			payloadGI.depth = 2;
			RayDesc rayX;
			rayX.Origin = pos;
			rayX.Direction = payloadGI.direction;
			rayX.TMin = 1e-3f;
			rayX.TMax = 10000.0;
			TraceRay(Scene, RAY_FLAG_NONE, ~0, 0, 2, 0, rayX, payloadGI);

			float3 L = payloadGI.direction;
			float3 H = normalize(L + V);
			float3 NdotL = saturate(dot(N, L));
			float3 LdotH = saturate(dot(L, H));
			float3 NdotH = saturate(dot(N, H));

			float diffuse_factor = Diffuse_Burley(NdotL, NdotV, LdotH, _Roughness);
			float3 specular_factor = Specular_BRDF(alpha, c_specular, NdotV, NdotL, LdotH, NdotH);

			//gi += payloadGI.color.rgb * dot(payloadGI.direction, N);
			gi += NdotL * payloadGI.color.rgb * (((c_diffuse * diffuse_factor / COO_PI) + specular_factor));
		}
		outputColor += gi / giSampleCount;
	}
	else
	{
		outputColor += IrradianceCube.SampleLevel(s0, N, 0) * g_skyBoxMultiple * c_diffuse;
	}

	if (payload.depth < 3)
	{
		RayPayload payloadReflect;
		payloadReflect.direction = reflect(-V, N);
		payloadReflect.color = float4(0, 0, 0, 0);
		payloadReflect.depth = payload.depth;
		RayDesc rayX;
		rayX.Origin = pos;
		rayX.Direction = payloadReflect.direction;
		rayX.TMin = 1e-3f;
		rayX.TMax = 10000.0;
		TraceRay(Scene, RAY_FLAG_NONE, ~0, 0, 2, 0, rayX, payloadReflect);

		outputColor += payloadReflect.color * surfaceReduction * Fresnel_Shlick(c_specular, grazingTerm, NdotV);
	}
	else
	{
		outputColor += EnvCube.SampleLevel(s0, reflect(-V, N), _Roughness * 6) * g_skyBoxMultiple * surfaceReduction * Fresnel_Shlick(c_specular, grazingTerm, NdotV);
	}
	outputColor *= AOFactor;
	outputColor += _Emission * _AmbientColor;

	payload.color = float4(payload.color.rgb + (1 - payload.color.a) * outputColor, payload.color.a + diffuseColor.a - payload.color.a * diffuseColor.a);

	RayDesc rayNext;
	rayNext.Origin = pos;
	rayNext.Direction = payload.direction;
	rayNext.TMin = 5e-4f;
	rayNext.TMax = 10000.0;
	if (payload.depth < 2 && payload.color.a < 1 - 1e-3f)
		TraceRay(Scene, RAY_FLAG_NONE, ~0, 0, 2, 0, rayNext, payload);
}

[shader("miss")]
void MissShaderSurface(inout RayPayload payload)
{
	payload.color += EnvCube.SampleLevel(s0, payload.direction, payload.depth) * g_skyBoxMultiple;
}


[shader("anyhit")]
void AnyHitShaderTest(inout TestRayPayload payload, in TriAttributes attr)
{
	VertexSkinned triVert[3];
	triVert[0] = Vertices[PrimitiveIndex() * 3];
	triVert[1] = Vertices[PrimitiveIndex() * 3 + 1];
	triVert[2] = Vertices[PrimitiveIndex() * 3 + 2];

	float2 uv = triVert[0].Tex * (1 - attr.barycentrics.x - attr.barycentrics.y) +
		triVert[1].Tex * (attr.barycentrics.x) +
		triVert[2].Tex * (attr.barycentrics.y);
	float4 diffuseColor = diffuseMap.SampleLevel(s0, uv, 0) * _DiffuseColor;
	if (diffuseColor.a < 0.5)
	{
		IgnoreHit();
	}
}

[shader("closesthit")]
void ClosestHitShaderTest(inout TestRayPayload payload, in TriAttributes attr)
{
	payload.miss = false;
	float3 pos = (Vertices[PrimitiveIndex() * 3].Pos * (1 - attr.barycentrics.x - attr.barycentrics.y) +
		Vertices[PrimitiveIndex() * 3 + 1].Pos * (attr.barycentrics.x) +
		Vertices[PrimitiveIndex() * 3 + 2].Pos * (attr.barycentrics.y)).xyz;
	float2 uv = (Vertices[PrimitiveIndex() * 3].Tex * (1 - attr.barycentrics.x - attr.barycentrics.y) +
		Vertices[PrimitiveIndex() * 3 + 1].Tex * (attr.barycentrics.x) +
		Vertices[PrimitiveIndex() * 3 + 2].Tex * (attr.barycentrics.y));

	payload.hitPos = pos;
	payload.color = diffuseMap.SampleLevel(s0, uv, 0) * _DiffuseColor;
}

[shader("miss")]
void MissShaderTest(inout TestRayPayload payload)
{
	payload.miss = true;
}
#endif // RAYTRACING_HLSL